group = "io.hyphenate"
archivesBaseName = "ease-call-kit"
version = project.sdkVersion//发布aar的库版本

apply {
    plugin 'maven-publish'
    plugin 'signing'
}


def localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

Properties properties = new Properties()
properties.load(file(localProperties.getProperty('maven.dir')+"./project.properties").newDataInputStream())

def ossrhUsername = properties.getProperty('ossrhUsername')
def ossrhPassword = properties.getProperty('ossrhPassword')

//指定singing的相关信息
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        def id = properties.getProperty("signing.keyId")
        def file = localProperties.getProperty('maven.dir')+properties.getProperty("signing.secretKeyRingFile")
        def password = properties.getProperty("signing.password")

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }
    }
}

configurations {
    deployerJars
}

task javadoc(type: Javadoc) {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
    }
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.source = variant.javaCompiler.source
            owner.classpath = files(android.bootClasspath.join(File.pathSeparator))
            owner.classpath += variant.javaCompiler.classpath
        }
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign publishing.publications
}


android {
    publishing {
        singleVariant('release') {
        }
    }
}

afterEvaluate {

    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = archivesBaseName
                version = project.sdkVersion

                afterEvaluate {
                    from components.release
                }
                // 设置要发布的文件
                artifact sourcesJar
                artifact javadocJar

                signing.sign publishing.publications.release

                pom {
                    name = "$archivesBaseName"

                    description = 'Easy call UI for Easemob IM SDK'
                    url = 'https://github.com/easemob/easecallkitui-android'

                    scm {
                        connection = 'scm:git@github.com:easemob/easecallkitui.git'
                        developerConnection = 'scm:git@github.com:easemob/easecallkitui.git'
                        url = 'https://github.com/easemob/easecallkitui-android.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'Easemob'
                            name = 'Easemob'
                            email = 'sdk-tools@easemob.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "ossrh"
                url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    getTasksByName("signReleasePublication", false).each { task ->
        task.mustRunAfter(sourcesJar)
        task.mustRunAfter(javadocJar)
    }

}


